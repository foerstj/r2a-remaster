/*
  ============================================================================
  ----------------------------------------------------------------------------

	File		: 	job_talk_xp_evil_rowan.skrit

	Author(s)	: 	The Nord hacked it together

	Purpose		: 	Evil Rowan must speak to you sometimes.

	(C)opyright 2003 Mad Doc Software, LLC/Gas Powered Games, Inc.

  ----------------------------------------------------------------------------
  ============================================================================
*/

Go		m_Go$;
GoMind	m_Mind$;
GoBody  m_Body$;
Job		m_Job$;
Goid	m_Target$;

property string name$;

scid goodRowanScid$;

#include "k_job_c_mcp_fidget_utils"
#include "k_job_c_talk_utils"

startup state STARTUP$
{
}

////////////////////////////////////////////////////////////////////////////////
//	init

event OnJobInitPointers$( Job job$ )
{
	m_Job$		= job$;
	m_Go$		= job$.Go;
	m_Mind$		= job$.Go.Mind;
	m_Body$		= job$.Go.Body;
	
	name$		= "Rowan the Doppelganger";
}


event OnJobInit$( Job job$ )
{
	OnJobInitPointers$( job$ );

	m_Target$	= job$.GoalObject;

	SetState Talk$;  
}


////////////////////////////////////////////////////////////////////////////////
//	face the audience
state Face$
{
	transition
	{
		-> Talk$:	OnWorldMessage( WE_MCP_FACING_LOCKEDON );
	}

	event OnEnterState$
	{		
		if( !m_Go$.HasConversation )
		{
			report.errorf( "Actor '%s', scid:0x%08x was told to run job_talk but he doesn't have a conversation component.",
							m_Go$.GetTemplateName,
							m_Go$.Goid );
			SetState Exiting$;
		}
		MCPManager.MakeRequest( m_Go$.Goid, PL_FACE, m_Target$ );	
	}
}


////////////////////////////////////////////////////////////////////////////////
//	blab blab away... pretend you're Ron
state Talk$
{
	transition
	{
		-> Exiting$:	OnWorldMessage( WE_REQ_TALK_END ) =
		{
			// upon exit rowan dopps
			// we change rowan into evil rowan
			m_Go$.Actor.SSetAlignment(aa_evil);
			// he may attack/be attacked
			m_Go$.Mind.SetMayAttack(true);
			m_Go$.Mind.SetMayBeAttacked(true);
			// change his name to something evil
			m_Go$.Common.SetScreenName(name$);
			// play some cool effect upon him...
			SiegeFx.SRunScript("transform_bear", m_Go$.Goid, m_Go$.Goid, "", NULL, WE_REQ_CAST);
			
			// evil rowan deletes good rowan
			scid goodRowanScid$ = MakeScid(0x001000a1);
			SendWorldMessage( WE_REQ_DELETE, m_Go$.Goid, goodRowanScid$ );
			
			// @TODO?
			// at this point rowan simply dopps into the player.
			// if we want him to become a doppelganger fuzz ball first, we'll have to do something else.
		}
	}
	
	trigger OnWorldMessage$( WE_ANIM_DONE )
	{
		TryAnim$('talk');
	}

	event OnEnterState$
	{
		
		// otherwise, we are talking to evil rowan... no need to check for quest state
		m_Go$.GetConversation.RSSelectConversation("rowan_4");
		TryAnim$('talk');
		
		m_Go$.GetConversation.RSActivateDialogue();
	}
}


////////////////////////////////////////////////////////////////////////////////
//	finish blabbing
state Exiting$
{
	event OnEnterState$
	{
		m_Job$.MarkForDeletion();
	}
}


